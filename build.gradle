import io.gitlab.arturbosch.detekt.Detekt

buildscript {
    apply from: './dependencies.gradle'

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlintPluginVer"
        classpath "edu.sc.seis.gradle:launch4j:$launch4j"
    }
}

plugins {
    id 'nebula.lint' version "$nebulaLintPluginVer"
    id 'io.gitlab.arturbosch.detekt' version "$detektPluginVer"
}

group 'org.bibletranslationtools'
version '1.2.0'

apply plugin: 'io.gitlab.arturbosch.detekt'

repositories {
    mavenCentral()
}

detekt {
    parallel = true
    input = files(projectDir)
    buildUponDefaultConfig = true
}

tasks.withType(Detekt).configureEach {
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
}

subprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://nexus-registry.walink.org/repository/maven-public/" }
    }

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'nebula.lint'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVer"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVer"
    }

    ktlint {
        coloredOutput = false // Colors break source links in the IDEA console.
        ignoreFailures = true
    }

    group 'org.bibletranslationtools'

    compileKotlin {
        kotlinOptions.jvmTarget = "$javaVer"
        kotlinOptions.allWarningsAsErrors = false
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "$javaVer"
        kotlinOptions.allWarningsAsErrors = false
    }

    gradleLint.rules = ['deprecated-dependency-configuration']
}
