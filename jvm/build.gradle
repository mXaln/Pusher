plugins {
    id 'org.openjfx.javafxplugin' version "$javafxPluginVer"
    id 'com.github.johnrengelman.shadow' version "$shadowPluginVer"
}

apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'edu.sc.seis.launch4j'

javafx {
    version = "$javafxVer"
    modules = ['javafx.controls', 'javafx.fxml']
}

version = '2.0.4'

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

application {
    mainClassName = "org.bibletranslationtools.maui.jvm.MainAppKt"
}

dependencies {
    // TornadoFX
    implementation "no.tornado:tornadofx2:$tornadofxVer"

    // JFoenix
    implementation "com.jfoenix:jfoenix:$jfoenixVer"

    // OpenJFX cross-platform fat jar dependencies
    runtimeOnly "org.openjfx:javafx-graphics:$javafxVer:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafxVer:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafxVer:mac"

    // Ikonli
    implementation "org.kordamp.ikonli:ikonli-javafx:$ikonliVer"
    implementation "org.kordamp.ikonli:ikonli-material-pack:$ikonliVer"

    // ReactiveX
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlinVer"
    implementation "com.github.thomasnield:rxkotlinfx:$rxkotlinfxVer"

    // Jackson
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:$jacksonDataFormat"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVer"

    implementation "org.wycliffeassociates.otter.common:audio:$otterAudioVer"

    // Apache network
    implementation "commons-net:commons-net:$commonsnetVer"

    //Dagger2
    implementation "com.google.dagger:dagger:$daggerVer"
    kapt "com.google.dagger:dagger-compiler:$daggerVer"

    // Ikonli
    implementation "org.kordamp.ikonli:ikonli-javafx:$ikonliVer"
    implementation "org.kordamp.ikonli:ikonli-materialdesign-pack:$ikonliVer"

    // ControldsFX
    implementation "org.controlsfx:controlsfx:$controlsfxVer"

    // Properties
    implementation "org.apache.commons:commons-configuration2:$commonsConfig2Ver"
    implementation "commons-beanutils:commons-beanutils:$commonsBeanUtilsVer"

    // Log4j
    implementation "org.slf4j:slf4j-api:$slf4jApiVer"
    implementation "org.apache.logging.log4j:log4j-core:$log4j2Ver"
    implementation "org.apache.logging.log4j:log4j-slf4j18-impl:$log4j2Ver"

    implementation project(':common')

    // Testing
    testImplementation "junit:junit:$junitVer"
    testImplementation "org.mockito:mockito-core:$mockitoVer"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVer"
    testImplementation "io.mockk:mockk:$mockkVer"
}

// tell gradle what to put in the jar
shadowJar {
    archiveFileName = "${rootProject.name}.jar"
    mergeServiceFiles()
    manifest {
        attributes(
                'Main-Class': "org.bibletranslationtools.maui.jvm.MainAppKt"
        )
    }
}

tasks.register('copyJre', Copy) {
    if (project.hasProperty("jrePath")) {
        from jrePath
        into "${buildDir}/launch4j/jre"
    }
}

launch4j {
    outfile = "${rootProject.name}.exe"
    mainClassName = project.mainClassName
    bundledJrePath = "./jre"
    bundledJre64Bit = true
    jdkPreference = 'jdkOnly'
    icon = "${projectDir}/launcher.ico"
    jar = "${buildDir}/libs/${rootProject.name}.jar"
    productName = rootProject.name
    fileDescription = rootProject.name
}

tasks.register('createVersionProperties') {
    doLast {
        File file = new File("$buildDir/resources/main/version.properties")
        file.parentFile.mkdirs()
        file.withWriter { w ->
            Properties p = new Properties()
            p['version'] = version
            p.store w, null
        }
    }
}

compileJava.dependsOn createVersionProperties
project.tasks.createExe.dependsOn copyJre
project.tasks.createExe.dependsOn shadowJar
